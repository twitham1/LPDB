@rem = '--*-Perl-*--
@echo off
if "%OS%" == "Windows_NT" goto WinNT
perl -x -S "%0" %1 %2 %3 %4 %5 %6 %7 %8 %9
goto endofperl
:WinNT
perl -x -S %0 %*
if NOT "%COMSPEC%" == "%SystemRoot%\system32\cmd.exe" goto endofperl
if %errorlevel% == 9009 echo You do not have Perl in your PATH.
if errorlevel 1 goto script_failed_so_exit_with_non_zero_val 2>nul
goto endofperl
@rem ';
#!/usr/bin/perl
#line 15

# picasagallery by twitham@sbcglobal.net, 2013-12

# GOAL: keyboard-driven mythgallery-style browser that understands Picasa:
#	albums, faces, stars, uploads, captions, exif, tags, location, etc.

# TODO: add filtering by flags, age
# TODO: add captions to thumbnails
# TODO: replace directory thumbnails with stacks of pictures & file count
# TODO: toggle option to replace face thumbnails with zoomed face crops
# TODO: auto-update the cache when dir/file timestamps are newer
# TODO: [random] slideshow below current point in tree w/ variable time
# TODO: pop-up menu of all options and/or keyboard shortcut help
# TODO: support all Picasa file formats (videos for example)

use strict;
use warnings;
#use POSIX qw/strftime/;
use Tk;
use Tk::Frame;
use Tk::JPEG;
use Tk::PNG;
use Tk::Bitmap;
use Tk::Pixmap;
use Tk::Photo;
use Tk::Font;
use Tk::Table;			 # for picture browser
use Data::Dumper;		 # for saving metadata cache
use File::Path;			 # for creating thumbnail paths
use Image::ExifTool qw(:Public); # for picture metadata
use Date::Calc qw/Delta_YMD Delta_Days/; # for age calculations
use Picasa;			 # for groking Picasa data!

my $picasa;			# picasa database

# config file can override any settings here:
our $conf = {
    config	=> '.picasagallery_conf.pl', # config to require, if present
    metadata	=> '.picasagallery_cache.pl', # cache of picture data
    cache	=> '.thumbcache', # where to put thumbnails
    keep	=> '(?i)\.(jpe?g|gif|x[pb]m|png|bmp|tiff?|nef)$',
    reject	=> '(Thumbs.db|desktop.ini)',
    datefmt	=> '%Y-%m-%d.%H:%M:%S', # must be sortable w/ year first
    update	=> \&update,		# don't change this
    debug	=> 0,			# debug to STDERR?
    info	=> 0,			# overlay metadata on images?
    width	=> 1280,		# initial size
    height	=> 720,
    fullscreen	=> 0, # go full-screen with no borders (ignore width/height)?
    age		=> {}, # optional contact(id|name) => 'yyyy/mm/dd' birthday
};

require $conf->{config} if -f $conf->{config};
$picasa = require $conf->{metadata} if -f $conf->{metadata};

my $current;	     # pointer to current point in picasa virtual tree
my $refreshing = ""; # directory currently refreshing

$Data::Dumper::Indent = 1;   # readable format for the cached metadata

my $mw = Tk::MainWindow->new (-title => "Picasa Gallery", -bg => 'black');
my($mx, $my) = ($conf->{width}, $conf->{height});
if ($conf->{fullscreen}) {
    ($mx, $my) = ($mw->screenwidth, $mw->screenheight);
    warn "going fullscreen to $mx x $my\n" if $conf->{debug};
    $mw->overrideredirect(1); # must be done before window is mapped
}
$mw->geometry(sprintf "%dx%d", $mx, $my);

mkdir $conf->{cache} or die $! unless -d $conf->{cache};

my $exif = new Image::ExifTool;
$exif->Options(FastScan => 1,
	       DateFormat => $conf->{datefmt});

my(@tn);		       # thumbnails: array of labels of Photos
my $tnx = 7;		       # columns of thumbnails to display
my $tpx = int(($mx - 4 * $tnx - 50) / $tnx); # 50 should be actual scroll bar width intead
my $tpy = int($tpx / 1.1);	# "average" aspect ratio for most pics

# text metadata in corners of browser
my($nw, $n, $ne, $se, $s, $sw) = ('', '', '', '', '', '');
# maybe font should be replaced on <configure> window resize...
my $big = $mw->Font(-family => 'Helvetica',
		    -size => int($my / 40), # 20,
		    -weight => 'bold',
    );
my $med = $mw->Font(-family => 'Helvetica',
		    -size => int($my / 50), # 15,
#		    -weight => 'bold',
    );

my @c = (-fg => 'white', -bg => 'black');
my $top = $mw->Frame(@c)->pack(-side => "top", -fill => 'x');
$top->Label(-textvariable => \$nw, -font => $med, @c)->pack(-side => "left");
$top->Label(-textvariable => \$ne, -font => $med, @c)->pack(-side => "right");
$top->Label(-textvariable => \$n,  -font => $big, @c, -fg => 'red')->pack(-side => "top");

my $bot = $mw->Frame(@c)->pack(-side => "bottom", -fill => 'x');
$bot->Label(-textvariable => \$sw, -font => $med, @c)->pack(-side => "left");
$bot->Label(-textvariable => \$se, -font => $med, @c)->pack(-side => "right");
$bot->Label(-textvariable => \$s,  -font => $med, @c, -fg => 'red')->pack(-side => "bottom");

# thumbnail table grid
my $tg = $mw->Table(-rows => 4,
		    -columns => 7,
		    -scrollbars => 'w',
		    -background => 'black',
    )->pack(-side => "top", -expand => 1, -fill => "both");

# full size image viewer
my $full = $mw->Toplevel(-width => $mx, -height => $my, -background => 'black');
$full->overrideredirect(1) if $conf->{fullscreen};
my $canvas = $full->Canvas(-width => $mx, -height => $my, -background => 'black',
			   -highlightthickness=> 0, -borderwidth => 0)
    ->pack(-side => 'top', -anchor => 'nw', -expand => 1, -fill => 'both');
my $image = $mw->Photo(-width => $mx, -height => $my);
$image->blank;
$canvas->createImage(0, 0, -image => $image, -anchor => 'nw');
$mw->raiseFocus;

$full->bind('<Configure>' => 	# resize window
	    sub  { my $w = shift;
		   ($mx, $my) = ($w->Width, $w->Height);
		   $canvas->configure(-width => $mx,
				      -height => $my);
		   $image->configure(-width => $mx,
				     -height => $my);
	    });
for my $w ($mw, $full) {
#    $w->bind('<KeyPress>', [\&processkey, Ev('A'), Ev('k'), Ev('K'), Ev('N')]);
    $w->bind($_, sub {		# up or exit
	my $tmp = $mw->focusCurrent;
	warn "focus=$tmp, mw=$mw, full=$full\n";
	if ($mw->focusCurrent eq $full) {
	    $mw->raiseFocus;
	} elsif ($picasa->{dir}{file} eq '/') {
	    $full->destroy;
	    $mw->destroy;
	    if (open my $fh, '>', "$conf->{metadata}~") {
		print $fh Dumper $picasa or die $!;
		close $fh and rename "$conf->{metadata}~",
		$conf->{metadata} or die $!;
	    }
	    exit;
	} else {
	    $picasa->up;
	    &newdir;
	}
	&move;
	     }) for (qw(<Key-q> <Escape>));
    $w->bind($_, sub {		# enter subdirectory or full picture
	if ($picasa->down) {
	    $mw->raiseFocus;
	    &newdir;
	} else {
	    $full->raiseFocus;
	    &newpic;
	}
	&move;
	     }) for (qw(<Key-d> <Return>));
    $w->bind($_, sub {
	$picasa->next;
	&newpic;
	&move;
	     }) for (qw(<Right> <space> <Key-n>));
    $w->bind($_, sub {
	$picasa->prev;
	&newpic;
	&move;
	     }) for (qw(<Left> <Up> <BackSpace> <Key-p>));
    $w->bind($_, sub {
	for (1 .. $tnx) { $picasa->next }
	&newpic;
	&move;
	     }) for (qw(<Down>));
    $w->bind($_, sub {
	for (1 .. $tnx) { $picasa->prev }
	&newpic;
	&move;
	     }) for (qw(<Up>));
    $w->bind($_, sub {
	$conf->{info} = $conf->{info} ? 0 : 1;
	&newpic;
	&move;
	     }) for (qw(<Key-i>));
}

unless ($picasa) {
    $picasa = Picasa->new($conf);
    $picasa->recursedirs('.');	# start finding the pictures
}

$mw->after(500, \&newdir);
$mw->raiseFocus;
MainLoop;

exit;

sub processkey {
    my($win, $ascii, $code, $str, $num) = @_;
    warn "'$ascii $code $str $num' was pressed\n";
}

BEGIN {	     # filtering is expensive, so we update only once a second
    my @child;
    my $updated = 0;
    sub update {
	$mw->update;		# let GUI work while we are busy
	return unless time > $updated;
	return unless $picasa->{root};
	return unless $picasa->{file};
	return unless $picasa->{file}{dir};
	return unless $picasa->{file}{file};

	warn "updating $File::Find::name\n" if $conf->{debug};
	my $loc = "$picasa->{file}{dir}$picasa->{file}{file}";
	$picasa->{file} = $picasa->filter($loc);
	&move;			# updates statistics
	$updated = time;
    }
}

sub nums {			# format the counts of attributes
    my $this = shift;
    return sprintf "%ds %du %df %da %dc",
    $this->{stars},
    $this->{uploads},
    scalar @{$this->{faces}},
    scalar @{$this->{albums}},
    $this->{caption} =~ /^\d+$/ ? $this->{caption} :
	$this->{caption} ? 1 : 0;
}

sub stats {			# format the [average] image metrics
    my($this, $x, $y) = @_;
    my $w = $this->{width} / $this->{files};
    my $h = $this->{height} / $this->{files};
    my $scale = $x / $w < $y / $h ? $x / $w : $y / $h;

    return sprintf "%.0f KB (%.2f MP) %.0f x %.0f (%.3f) %.0f%%",
    $this->{bytes} / 1024 / $this->{files},
    $this->{pixels} / 1000 / 1000 / $this->{files},
    $w, $h, $w / $h, 100 * $scale;
}

sub move {			# move to new directory or file
    return unless $picasa->{index} >= 0;

    $mw->title("PG: $picasa->{file}{dir}");
    $tg->see(int($picasa->{index} / $tnx + 1), # Table index begins at 1, not 0!
	     $picasa->{index} % $tnx + 1);

    return unless $current = $picasa->{file};
    warn "location: $picasa->{dir}{dir} $picasa->{dir}{file} - $picasa->{file}{dir} $picasa->{file}{file} ($picasa->{index})\n" if $conf->{debug};
    return unless $current->{files};


    for my $i (0 .. $#tn) {
	my $w = $tg->get(int($i / $tnx + 1), $i % $tnx + 1);
	$w->configure(-bg => 'black');
	$w->configure(-bg => 'white') if $i == $picasa->{index};
    }

    $n = $current->{file};
    $n =~ s/$current->{time}-// unless $n =~ m!/$!;

    $nw = sprintf "(%s) %.2f MB (%.2f GP) in %d files",
    &nums($current),
    $current->{bytes} / 1024 / 1024,
    $current->{pixels} / 1000 / 1000 / 1000,
    $current->{files};

    $ne = &stats($current, $tpx, $tpy);
    $s = $current->{dir};

    # my @t = split /\D+/, $current->{time};
    # $t[0] -= 1900; $t[1]--;
    # $sw = strftime "%x", @t;
    $sw = $current->{time};
    $se = $current->{time} eq $current->{endtime} ?
	$current->{caption} || '' : $current->{endtime};
}

sub newdir {		 # replace all thumbnails in current directory
    my $now = "$picasa->{dir}{dir}$picasa->{dir}{file}";
    $now =~ s@/+@/@g;
    $refreshing eq $now and return;
    warn "\n\nnewdir $now\n" if $conf->{debug};
    $refreshing = $now;
    $mw->raiseFocus;

    # Gather all pics in this folder
    $current = $picasa->{dir};
    my $index = 0;
    foreach my $img (@{$current->{children}}) {
	my $now = "$picasa->{dir}{dir}$picasa->{dir}{file}";
	$now =~ s@/+@/@g;
	$refreshing eq $now or return;

        my $child = "$now$img";
	my $this = $picasa->filter($child) or next;
	my $pf = $this->{physical} or next;
        my $ps = -s $pf or next;
	my $data = $picasa->{pics}{$pf} or next; # data of this picture
	my $x = $data->{width} or next;
	my $y = $data->{height} or next;

	unless ($tn[$index]) { # connect 1 photo to each cell only once
	    my $cell = $tg->Label(-width => $tpx, -height => $tpy, -image =>
				  $mw->Photo(-width => $tpx, -height => $tpy));
	    $tn[$index] = $cell;
	    $tg->put(int($index / $tnx) + 1, # Table index begins at 1, not 0!
		     $index % $tnx + 1,
		     $cell);
	}
        my $scale = $tpy / $y < $tpx / $x ? $tpy / $y : $tpx / $x;
	my $image = $tn[$index]->cget(-image); # replace cell's photo
	$image->PhotoXYthumb($pf, map { int } ($scale * $x, $scale * $y));

#	warn "putting $index / $tnx ($w in $tg - $t - $pf)\n" if $conf->{debug} > 1;
        $tn[$index]->update;	# Display the thumbnail
	$index++;
    }
    while ($index <= $#tn) {	# blank the unused cells
    	$tn[$index++]->cget(-image)->blank;
    }
    &move;
    $refreshing = "";
}

sub newpic {	     # replace the full picture with current selection
    $full->focusCurrent eq $full or return;
    my $this = $picasa->{file} or return;
    my $pf = $this->{physical} or return;
    my $ps = -s $pf or return;
    my $data = $picasa->{pics}{$pf} or return; # data of this picture
    my $x = $data->{width} or return;
    my $y = $data->{height} or return;
    my $scale = $mx / $x < $my / $y ? $mx / $x : $my / $y;
    my($X, $Y) = $image->PhotoXYfull($pf, map { int } $scale * $x, $scale * $y)
	or return;
    $full->title("PG: $picasa->{file}{file}");

    $canvas->delete('exif');
    $canvas->delete('caption');
    $canvas->delete('info');
    $canvas->delete('faces');
    return unless $conf->{info};

    if (my $info = $exif->ImageInfo($pf)) {
	my @info;
	push @info, $info->{Make} if $info->{Make};
	push @info, $info->{Model} if $info->{Model};
	push @info, "$info->{ExposureTime}s" if $info->{ExposureTime};
	push @info, $info->{FocalLength} if $info->{FocalLength};
	push @info, "($info->{FocalLengthIn35mmFormat})" if $info->{FocalLengthIn35mmFormat};
	push @info, "f/$info->{FNumber}" if $info->{FNumber};
	push @info, "ISO: $info->{ISO}" if $info->{ISO};
	push @info, $info->{Flash} if $info->{Flash};
	$canvas->createTextBox($mx - 2, $my - 2, 'black',
			       -justify => 'right',
			       -anchor => 'se',
			       -font => $med,
			       -fill => 'white',
			       -text => (join " \n", @info),
			       -tags => 'exif');
    }
    $canvas->createTextBox($mx, 0, 'black',
			   -justify => 'right',
			   -anchor => 'ne',
			   -font => $med,
			   -fill => 'white',
			   -text => &stats($this, $mx, $my),
			   -tags => 'info');
    my $text = &nums($this);
    (my $file = $this->{file}) =~ s/$this->{time}-//;
    $text = "$this->{dir}\n$file\n($text)\n$this->{time}";
    $text .= "\n$this->{endtime}" if $this->{time} ne $this->{endtime};;
# TODO: add total statistics here for directories
#    $text .= "\n$this->{files} files\n$nw" if $this->{files} > 1;
    $canvas->createTextBox(0, 0, 'black',
			   -anchor => 'nw',
			   -font => $med,
			   -fill => 'white',
			   -text => $text,
			   -tags => 'info');
    $text = join "\n", map { $picasa->{album}{$_}{name} } @{$this->{albums}};
    $canvas->createTextBox(0, $my, 'black',
			   -anchor => 'sw',
			   -font => $med,
			   -fill => 'white',
			   -text => $text,
			   -tags => 'info');
    if ($this->{caption}) {
	$canvas->createTextBox($mx / 2, $my, 'black',
			       -anchor => 's',
			       -font => $med,
			       -fill => 'white',
			       -text => $this->{caption},
			       -tags => 'caption');
    }

    for my $f (@{$this->{faces}}) { # named rectangles around faces!
	next unless ref $f;
	my($id, $l, $t, $r, $b) = @$f;
	my $name = $picasa->contact2person($id);
	$canvas->createRectangle($X + $l * $x * $scale, $Y + $t * $y * $scale,
				 $X + $r * $x * $scale, $Y + $b * $y * $scale,
				 -outline => 'magenta',
				 -tags => 'faces');
	$canvas->createText($X + $l * $x * $scale + 2, $Y + $t * $y * $scale,
			    -anchor => 'nw',
			    -font => $big,
			    -fill => 'magenta',
			    -text => $name,
			    -tags => 'faces');
	if (my $birth = $conf->{age}{$id} # option to show ages of faces :-)
	    || $conf->{age}{$name}) {
	    my($yr, $m, $d) = split '/', $birth;
	    my($Yr, $M, $D) = split /\D+/, $this->{time};
	    my($year, $mon, $day) = Delta_YMD($yr, $m, $d, $Yr, $M, $D);
	    warn "age $id/$name: $Yr, $M, $D - $yr, $m, $d = $year, $mon, $day\n"
		if $conf->{debug};
	    my $age;
	    if ($year < 1 && $mon < 3) {
		$age = sprintf "%dd", Delta_Days($yr, $m, $d, $Yr, $M, $D);
	    } elsif ($year < 3) {
		$age = sprintf "%dm", int($year * 12 + $mon);
	    } else {
		$age = sprintf "%d", int($year + $mon / 12 + $day / 365);
	    }
	    $canvas->createText($X + $r * $x * $scale - 2, $Y + $b * $y * $scale,
				-justify => 'right',
				-anchor => 'se',
				-font => $big,
				-fill => 'magenta',
				-text => $age,
				-tags => 'faces');
	}
    }
}

# read $f file into $w image scaled to size $x / $y, with cacheing
sub Tk::PhotoXYthumb {
    my ($w, $f, $x, $y, $c) = @_;
    $f && $x && $y or return;
    $w->blank;
    my $ext = 'jpg';		# thumnail format
    my $cfn = "$conf->{cache}/$f-$x-$y.$ext";
    unless (-f $cfn && -M _ < -M $f) {
	my($dir, $file) = Picasa::dirfile($cfn);
	-d $dir or mkpath $dir or warn "can't mkdir $dir: $!\n";
	my $geo = "${x}x${y}";
	if ($c) {
# TODO: add captions:
	    system qw/convert -background #0006 -fill white -gravity center -size/,
	    $tpx . 'x' . int($tpy / 6), "caption:$c", $f, '-thumbnail',  $geo,
	    qw/+swap -gravity south -composite/, $cfn;
	} else {
	    system qw/convert -size/, $geo, '-thumbnail', $geo, $f, $cfn;
	}
    }
    $w->read($cfn, -to => int(($tpx - $x) / 2), int(($tpy - $y) / 2));
}

# read $f file into $w image scaled to size $x / $y
BEGIN {
    my $id = 10000;
    my $ext = 'jpg';		# full format
    sub Tk::PhotoXYfull {
	my ($w, $f, $x, $y) = @_;
	$f && $x && $y or return;
	$w->blank;
	my $cfn = "$conf->{cache}/tmp-$$-" . $id++ . ".$ext";
	my $geo = "${x}x${y}";
	system "convert", "-size", $geo, "-resize", "$geo+0+0", $f, $cfn;
	my($l, $t) = (int(($mx - $x) / 2), int(($my - $y) / 2));
	$w->read($cfn, -to => $l, $t);
	unlink $cfn;
	return $l, $t;
    }
}

# createText on a box of given color $c
sub Tk::Canvas::createTextBox {
    my($w, $x, $y, $c, %opt) = @_;
    my $t = $w->createText($x, $y, %opt);
    my @opt;
    push @opt, (-tags => $opt{'-tags'}) if $opt{'-tags'};
    $w->createRectangle($w->bbox($t), -fill => $c, @opt);
    $w->raise($t);
}

# raise window $w and ensure it has focus
sub Tk::raiseFocus {
    my($w) = @_;
    $w->deiconify;
    $w->raise;
    if ($conf->{fullscreen}) {	# force needed on Linux
	$w->focusForce;		# with overrideredirect(1)
	$w->grabGlobal;
    }
}

__END__
:endofperl
