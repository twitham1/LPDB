@rem = '--*-Perl-*--
@echo off
if "%OS%" == "Windows_NT" goto WinNT
perl -x -S "%0" %1 %2 %3 %4 %5 %6 %7 %8 %9
goto endofperl
:WinNT
perl -x -S %0 %*
if NOT "%COMSPEC%" == "%SystemRoot%\system32\cmd.exe" goto endofperl
if %errorlevel% == 9009 echo You do not have Perl in your PATH.
if errorlevel 1 goto script_failed_so_exit_with_non_zero_val 2>nul
goto endofperl
@rem ';
#!/usr/bin/perl
#line 15

# picasagallery by twitham@sbcglobal.net, 2013-12

# keyboard-driven mythgallery-style browser that understands local Picasa:
# albums, faces, stars, uploads, captions, exif, tags, location, etc.

# TODO: [random] slideshow below current point in tree w/ variable time
# TODO: toggle option to replace face thumbnails with zoomed face crops
# TODO: support all Picasa file formats (videos for example) (help ->
#	options -> file types: jpe?g, bmp, gif, png, tga, tiff?, webp,
#	psd, (many raw), (movies: avi, mpg, asf, wmv)
# TODO: add zoom/pan to image viewer

use strict;
use warnings;
use Tk;
use Tk::Frame;
use Tk::JPEG;
use Tk::PNG;
use Tk::Bitmap;
use Tk::Pixmap;
use Tk::Photo;
use Tk::Font;
use Tk::Table;			 # for picture browser
use Tk::ProgressBar;		 # for metadata refreshing progress bar
use Data::Dumper;		 # for saving metadata cache
use File::Path;			 # for creating thumbnail paths
use Image::ExifTool qw(:Public); # for picture metadata
use Date::Calc qw/Delta_YMD Delta_Days/; # for age calculations
use Picasa;			 # for groking Picasa data!

my $picasa;			# working picasa database
my $picasa_tmp;			# picasa database while updating

# config file can override any settings here (Picasa.pm also uses a few):
our $conf = {
    config	=> '.picasagallery_conf.pl', # config to require, if present
    metadata	=> '.picasagallery_cache.pl', # cache of picture data
    cache	=> '.thumbcache', # where to put thumbnails
    keep	=> '(?i)\.(jpe?g|gif|x[pb]m|png|bmp|tiff?|nef)$',
    reject	=> '(?i)\b(thumbs?(.db)?)\b',
    datefmt	=> '%Y-%m-%d %H:%M:%S %a', # must be sortable w/year first
    update	=> \&update,		# don't change this (GUI updates)
    debug	=> 0,			# debug to STDERR?
    info	=> 0,			# overlay metadata on images?
    width	=> 1280,		# initial size
    height	=> 720,
    fullscreen	=> 0, # go full-screen with no borders (ignore width/height)?
    birthday	=> {}, # optional contact(id|name) => 'yyyy/mm/dd' birthdays
    bg		=> 'black',	# background color for the application
    select	=> 'green',	# current thumbnail selection color
    text	=> 'white',	# corner information text color
    title	=> 'red',	# header/footer color
    face	=> 'green',  # face box color (uncommon picture color)
    filtering	=> 0,	     # how many filters are in effect
    filters	=> [qw/Stars Uploads Faces Albums Captions Tags/],
    filter	=> {
	age => 0,	   # show only newer than X seconds before now
    },
};

require $conf->{config} if -f $conf->{config}; # start with previous run data:
$picasa = require $conf->{metadata} if -f $conf->{metadata};

my $refreshing = '';	   # directory currently refreshing thumbnails
my %rebuildthumb;	   # stale thumbnails to rebuild

$Data::Dumper::Indent = 1;   # readable format for the cached metadata

mkdir $conf->{cache} or die $! unless -d $conf->{cache}; # thumbnail cache

my $exif = new Image::ExifTool;	# for collecting picture metadata
$exif->Options(FastScan => 1,
	       DateFormat => $conf->{datefmt});

# main window is a thumbnail browser ----------------------------------------
my $mw = Tk::MainWindow->new (-title => "Picasa Gallery", -bg => $conf->{bg});
my $cur = $mw;			# current window
my($mx, $my) = ($conf->{width}, $conf->{height});
$mw->geometry(sprintf "%dx%d", $mx, $my);
$mw->update;
$conf->{fullscreen} and &togglefullscreen($mw);

my(@tn);		       # thumbnails: array of labels of Photos
my $tnx = 7;		       # columns of thumbnails to display
my $tpx = int(($mx - 4 * $tnx - 50) / $tnx); # 50 should be actual scroll bar width intead
my $tpy = int($tpx / 1.1);	# "average" aspect ratio for most pics

# text metadata in corners of browser
my($nw, $n, $ne, $se, $s, $sw) = ('', '', '', '', '', '');
# maybe font should be replaced on <configure> window resize...
my $big = $mw->Font(-family => 'Helvetica',
		    -size => int($my / 40), # 20,
		    -weight => 'bold',
    );
my $med = $mw->Font(-family => 'Helvetica',
		    -size => int($my / 50), # 15,
    );

my @c = (-fg => $conf->{text}, -bg => $conf->{bg});
my $top = $mw->Frame(@c)->pack(-side => "top", -fill => 'x');
my $nww = $top->Label(-textvariable => \$nw, -font => $med, @c)->pack(-side => "left");
my $new = $top->Label(-textvariable => \$ne, -font => $med, @c)->pack(-side => "right");
$top->Label(-textvariable => \$n,  -font => $big, @c, -fg => $conf->{title})->pack(-side => "top");

my $bot = $mw->Frame(@c)->pack(-side => "bottom", -fill => 'x');
$bot->Label(-textvariable => \$sw, -font => $med, @c)->pack(-side => "left");
$bot->Label(-textvariable => \$se, -font => $med, @c)->pack(-side => "right");
$bot->Label(-textvariable => \$s,  -font => $med, @c, -fg => $conf->{title})->pack(-side => "bottom");

# thumbnail table grid
my $tg = $mw->Table(-rows => 4,
		    -columns => 7,
		    -scrollbars => 'w',
		    -bg => $conf->{bg},
    )->pack(-side => "left", -expand => 1, -fill => "both");

# metadata refreshing progress bar
my $sofar = 0;
my $pg = $mw->ProgressBar(-anchor => 's', -to => $picasa->{pics} ?
			  scalar keys %{$picasa->{pics}} : 1000,
			  -variable => \$sofar,
			  -gap => 0,
			  -troughcolor => 'red',
    )->pack(-side => "right", -expand => 0, -fill => 'y');

# full size image viewer --------------------------------------------------
my $full = $mw->Toplevel(-width => $mx, -height => $my, -bg => $conf->{bg});
$full->geometry(sprintf "%dx%d", $mx, $my);
$full->update;
$conf->{fullscreen} and &togglefullscreen($full);
my $canvas = $full->Canvas(-width => $mx, -height => $my, -bg => $conf->{bg},
			   -highlightthickness=> 0, -borderwidth => 0)
    ->pack(-side => 'top', -anchor => 'nw', -expand => 1, -fill => 'both');
my $image = $mw->Photo(-width => $mx, -height => $my);
$image->blank;
$canvas->createImage(0, 0, -image => $image, -anchor => 'nw');

# popup option menu is invoked with "m" key ----------------------------------
my $menu = $mw->Menu(-tearoff => 0, -takefocus => 1, # takefocus doesn't work???
		     -font => $med);
$menu->transient($mw);		# not needed?
$menu->add('command', -label => 'Menu: Picasa Gallery',
	   -accelerator => 'm', -underline => 0, -state => 'disabled');
$menu->add('checkbutton', -label => 'Info Overlay',
	   -accelerator => 'i', -underline => 0,
	   -variable => \$conf->{info}, -command => \&newpic);
$menu->add('separator');
my $i = 0;
$menu->add('command', -label => "Clear All Filters",
	   -accelerator => $i++, -underline => 0, -command =>
	   sub { $mw->eventGenerate('<Key-0>') });
my $filter = $menu->Menu(-tearoff => 0, -font => $med);
for (@{$conf->{filters}}) {
    $filter->add('checkbutton', -label => $_,
		 -accelerator => $i++,
		 -variable => \$conf->{filter}{$_},
		 -onvalue => 1,
		 -offvalue => 0,
		 -underline => 0,
		 -command => sub {
		     $picasa->filtermove;
		     &newdir;
		     &newpic;
		     &move;
		 });
}
$menu->add('cascade', -label => 'Filter by Attributes', -menu => $filter,
	   -underline => 0);
my $age = $menu->Menu(-tearoff => 0, -font => $med);
my $day = 24 * 60 * 60;
my $year = 365.25 * $day;	# estimate, close enough
my %age = my @age		# ugly menu, but any better way?
    = (			 # maybe a slider widget, but where to put it?
    'unlimited'	=> 0,
    '20 years'	=> 20 * $year,
    '15 years'	=> 15 * $year,
    '10 years'	=> 10 * $year,
    '7 years'	=> 7 * $year,
    '5 years'	=> 5 * $year,
    '3 years'	=> 3 * $year,
    '2 years'	=> 2 * $year,
    '1 year'	=> 1 * $year,
    '9 months'	=> 9 / 12 * $year,
    '7 months'	=> 7 / 12 * $year,
    '5 months'	=> 5 / 12 * $year,
    '3 months'	=> 3 / 12 * $year,
    '2 months'	=> 2 / 12 * $year,
    '7 weeks'	=> 7 * 7 * $day,
    '5 weeks'	=> 5 * 7 * $day,
    '3 weeks'	=> 3 * 7 * $day,
    '2 weeks'	=> 2 * 7 * $day,
    '7 days'	=> 7 * $day,
    '5 days'	=> 5 * $day,
    '3 days'	=> 3 * $day,
    '2 days'	=> 2 * $day,
    );
{
    my $last;
    for (@age) {
	$age{$_} = $last and next if /^\d+$/;
	$age->add('radiobutton',
		  -label => $_,
		  -variable => \$conf->{filter}{age},
		  -value => $age{$_},
		  (/^\d year|unlimited/ ? (-underline => 0) : ()),
		  -command => sub {
		      $picasa->filtermove;
		      &newdir;
		      &newpic;
		      &move;
		  });
	$last = $_;
    }
}
$menu->add('cascade', -label => 'Filter by Age',
	   -menu => $age, -underline => 10);
$menu->add('separator');
$menu->add('command', -label => 'Rebuild Thumbnail',
	   -underline => 0, -command =>
	   sub { 	
	       my $tmp = "$picasa->{file}{dir}$picasa->{file}{file}";
	       $tmp =~ s@/$@@;
	       $rebuildthumb{$tmp} = 1;
	       &newdir;
	   });
$menu->add('command', -label => 'Quit', -underline => 0, -command =>
	   sub { while(1) { $mw->eventGenerate('<Escape>'); } });

$menu->bind('<Unmap>', sub { $menu->withdraw; $cur->raiseFocus; });

$mw->raiseFocus;

# all event bindings happen here ----------------------------------------
$full->bind('<Configure>' => # resize window resizes full-size picture
	    sub  { my $w = shift;
		   ($mx, $my) = ($w->Width, $w->Height);
		   $canvas->configure(-width => $mx,
				      -height => $my);
		   $image->configure(-width => $mx,
				     -height => $my);
		   &newpic;
	    });
$mw->bind('<Button-1>' => [\&click, Ev('W') ]);
sub click {			# move selection or enter selection
    my($w) = @_;
    my($r, $c) = $tg->Posn($w);
    return unless $r and $c;
    my $cur = $picasa->{index};
    $picasa->{index} = --$r * $tnx + --$c;
    $picasa->filtermove;
    &move;
    $picasa->{index} == $cur and $mw->eventGenerate('<Return>');
}

for my $w ($mw, $full) {	# both windows have same key bindings

    $w->bind('<KeyPress>', [\&processkey, Ev('A'), Ev('k'), Ev('K'), Ev('N')]);

    for (qw(<F11>)) {
	$w->bind($_, sub {	# toggle full screen display
	    &togglefullscreen($w);
		 });
    }

    for (qw(<Key-m> <Button-3>)) {
	$w->bind($_, sub {	# popup options menu
	    $menu->post($w->x + $w->width / 3, $w->y + $w->height / 3);
	    $menu->raiseFocus;	# -takefocus fails on mythbuntu???
		 });
    }
    for (qw(<Key-q> <Escape> <Button-2>)) {
	$w->bind($_, sub {	# up or exit
	    my $tmp = $mw->focusCurrent;
	    if ($mw->focusCurrent eq $full) {
		$mw->raiseFocus;
		&move;
	    } elsif ($picasa->{dir}{file} eq '/') {
		$full->destroy;
		$mw->destroy;
		if ($picasa->{done} and
		    open my $fh, '>', "$conf->{metadata}~") {
		    print $fh Dumper $picasa or die $!;
		    close $fh and rename "$conf->{metadata}~",
		    $conf->{metadata} or die $!;
		}
		Tk::exit;
	    } else {
		$picasa->up;
		&newdir;
	    }
		 });
    }
    for (qw(<Key-d> <Return>)) {
	$w->bind($_, sub {	# enter subdirectory or full picture
	    if ($picasa->down) {
		$mw->raiseFocus;
		&newdir;
	    } else {
		$full->raiseFocus;
		&newpic;
	    }
		 });
    }
    for (qw(<Right> <space> <Key-n>)) {
	$w->bind($_, sub {	# next picture or subdir
	    $picasa->next;
	    &newpic;
		 });
    }
    for (qw(<Left> <Up> <BackSpace> <Key-p>)) {
	$w->bind($_, sub {	# previous picture or subdir
	    $picasa->prev;
	    &newpic;
		 });
    }
    for (qw(<Down>)) {
	$w->bind($_, sub {	# next row
	    $picasa->next($tnx);
	    &newpic;
		 });
    }
    for (qw(<Up>)) {
	$w->bind($_, sub {	# previous row
	    $picasa->prev($tnx);
	    &newpic;
		 });
    }
    for (qw(<Next>)) {
	$w->bind($_, sub {	# next page
	    $picasa->next($tnx * 4);
	    &newpic;
		 });
    }
    for (qw(<Prior>)) {
	$w->bind($_, sub {	# previous page
	    $picasa->prev($tnx * 4);
	    &newpic;
		 });
    }
    for (qw(<Key-i>)) {
	$w->bind($_, sub {	# information toggle
	    $conf->{info} = $conf->{info} ? 0 : 1;
	    &newpic;
		 });
    }
}

# all metadata is updated in the background ----------------------------------------
warn "starting Picasa recursion\n" if $conf->{debug};
$picasa_tmp = Picasa->new($conf); # rebuild all data since prior run
$picasa or $picasa = $picasa_tmp; # keep previous run or use first run live
$mw->after(500, \&newdir);	# show browser after we have some data
$picasa_tmp->recursedirs('.');	# updates GUI via $conf->{update}
warn "Picasa recursion done!\n" if $conf->{debug};

if ($picasa_tmp->{done} && $picasa ne $picasa_tmp) {
    warn "replacing $picasa with $picasa_tmp\n" if $conf->{debug};
    my $old = $picasa;
    $picasa_tmp->{index} = $picasa->{index}; # retain current location
    $picasa_tmp->{dir} = $picasa->{dir};
    $picasa_tmp->{file} = $picasa->{file};
    $picasa = $picasa_tmp;
    undef $old;			# free previous data, now stale
}
$pg->value($pg->cget('-to') + 10);
&move;				# update stats one last time

MainLoop;			# never returns
Tk::exit;			# should never get here

sub processkey {  # generic key event handler if nothing more specific
    my($win, $ascii, $code, $str, $num) = @_;
    warn "'$ascii $code $str $num' was pressed\n";
    if ($ascii =~ /^\d+$/) {
	if ($ascii == 0) {	# clear all filters
	    map { $conf->{filter}{$_} = 0 } keys %{$conf->{filter}};
	} elsif (defined $conf->{filters}[--$ascii]) { # toggle filter
	    $conf->{filter}{$conf->{filters}[$ascii]} = 
		! $conf->{filter}{$conf->{filters}[$ascii]};
	}
	$picasa->filtermove;	# update all metadata
	&newdir;
	&newpic;
	&move;
    }
}

BEGIN {	# filtering for stats is expensive, so we update GUI only once a second
    my $updated = 0;
    sub update {
	$mw->update;		# let GUI work while we are busy
	return unless time > $updated;
	return unless $picasa_tmp->{root};
	return unless $picasa_tmp->{file};
	return unless $picasa_tmp->{file}{dir};
	return unless $picasa_tmp->{file}{file};

	warn "updating $File::Find::name\n" if $conf->{debug};
	my $loc = "$picasa_tmp->{file}{dir}$picasa_tmp->{file}{file}";
	$picasa_tmp->{file} = $picasa_tmp->filter($loc);
	&move;			# updates statistics on GUI
	$sofar = keys %{$picasa_tmp->{pics}};
	if ($sofar > $pg->cget('-to')) {
	    $pg->configure(-to => $sofar + 1000);
	}
	$updated = time;
    }
}

sub nums {	# format the counts of attributes; upcase if filtering
    my $this = shift;
    my $out = sprintf "%ds %du %df %da %dc %dt",
    $this->{stars},
    $this->{uploads},
    scalar @{$this->{faces}},
    scalar @{$this->{albums}},
    $this->{caption} =~ /^\d+$/ ? $this->{caption} : $this->{caption} ? 1 : 0,
    scalar @{$this->{tags}};
    $conf->{filtering} = scalar
	grep { $conf->{filter}{$_} and /^([A-Z])/ and $out =~ s/($1)/\U$1\E/i }
    keys %{$conf->{filter}};
    $conf->{filter}{age} and ++$conf->{filtering} and
	$out .= " $age{$conf->{filter}{age}}";
    $nww->configure($conf->{filtering} ? (-fg => 'white', -bg => 'red') : @c);
    return ($conf->{filtering} || $out =~ /[1-9]/ ? $out : '');
}

sub stats {			# format the [average] image metrics
    my($this, $x, $y) = @_;
    my $w = $this->{width} / $this->{files};
    my $h = $this->{height} / $this->{files};
    my $scale = $x / $w < $y / $h ? $x / $w : $y / $h;
    $scale *= 2 / 3 if $this->{file} =~ m!/$!;

    return sprintf "%.0f KB (%.2f MP) %.0f x %.0f (%.3f) %.0f%%",
    $this->{bytes} / 1024 / $this->{files},
    $this->{pixels} / 1000 / 1000 / $this->{files},
    $w, $h, $w / $h, 100 * $scale;
}

sub move {			# move to new directory or file
    return unless $picasa->{index} >= 0;

    $mw->title("PG: $picasa->{file}{dir}");
    $tg->see(int($picasa->{index} / $tnx + 1), # Table index begins at 1, not 0!
	     $picasa->{index} % $tnx + 1);

    return unless my $current = $picasa->{file};
    warn "location: $picasa->{dir}{dir} $picasa->{dir}{file} - $picasa->{file}{dir} $picasa->{file}{file} ($picasa->{index})\n" if $conf->{debug};
    return unless $current->{files};

    for my $i (0 .. $#tn) {
	my $w = $tg->get(int($i / $tnx + 1), $i % $tnx + 1);
	$w->configure(-bg => $conf->{bg});
	$w->configure(-bg => $conf->{select}) if $i == $picasa->{index};
    }

    $n = $current->{file};
    $n =~ s/$current->{time}-//; # unless $n =~ m!/$!;

    $nw = sprintf "%.2f MB (%.2f GP) in %d files",
    $current->{bytes} / 1024 / 1024,
    $current->{pixels} / 1000 / 1000 / 1000,
    $current->{files};
    my $tmp = &nums($current);
    $nw .= " ($tmp)" if $tmp;

    $ne = &stats($current, $tpx, $tpy); # highlight here means update is in progress
    $new->configure($picasa_tmp->{done} ? @c : (-fg => 'white', -bg => 'red'));

    my $total = @{$picasa->{dir}{children}};
    my $cur = $picasa->{index} + 1;
    $s = "$current->{dir}: $cur / $total";

    $sw = $current->{time};
    $se = $current->{time} eq $current->{endtime} ?
	$current->{caption} || '' : $current->{endtime};
}

sub newdir {		 # replace all thumbnails in current directory
    my $now = "$picasa->{dir}{dir}$picasa->{dir}{file}";
    $now =~ s@/+@/@g;
    $refreshing eq $now and return;
    warn "\n\nnewdir $now\n" if $conf->{debug};
    $refreshing = $now;
    $mw->raiseFocus;

    # Gather all pics in this folder
    my $index = 0;
    &move;
    foreach my $img (@{$picasa->{dir}{children}}) {
	my $now = "$picasa->{dir}{dir}$picasa->{dir}{file}";
	$now =~ s@/+@/@g;	# bail out if user moved location
	$refreshing eq $now or return;

        my $child = "$now$img";
	my $this = $picasa->filter($child, 'nofilter') or next;
	my $pf = $this->{physical} or next;
        my $ps = -s $pf or next;
	my $data = $picasa->{pics}{$pf} or next; # data of this picture
	my $x = $data->{width} or next;
	my $y = $data->{height} or next;

	unless ($tn[$index]) { # connect 1 photo to each cell only once
	    $tn[$index] = $tg->Label(-width => $tpx, -height => $tpy, -image =>
				     $mw->Photo(-width => $tpx, -height => $tpy),
				     -bg => $conf->{bg});
	    $tg->put(int($index / $tnx) + 1, # Table index begins at 1, not 0!
		     $index % $tnx + 1,
		     $tn[$index]);
	}
	my $image = $tn[$index]->cget(-image); # replace cell's photo
	if ($img =~ m!/$!) {	# directory: stack some pics
	    $child =~ s@/$@@;
	    $image->StackXYthumb($child, $tpx, $tpy, "$this->{files}: $img",
				 $this->{first}, $this->{physical}, $this->{'last'});
	} else {
	    my $scale = $tpy / $y < $tpx / $x ? $tpy / $y : $tpx / $x;
	    $image->PhotoXYthumb($pf, (map { int } $scale * $x, $scale * $y),
				 $data->{caption} || 0);
	}

#	warn "putting $index / $tnx ($w in $tg - $t - $pf)\n" if $conf->{debug} > 1;
        $tn[$index]->update;	# Display the thumbnail
	$index++;
    }
    while ($index <= $#tn) {	# blank the unused cells
    	$tn[$index++]->cget(-image)->blank;
    }
    &move;
    $refreshing = "";
}

sub newpic {	     # replace the full picture with current selection
    &move;
    $full->focusCurrent eq $full or return;
    my $this = $picasa->{file} or return;
    if ($this->{file} =~ m!/$!) { # raise browser to see directory option
	&newdir;
	return;
    }
    my $pf = $this->{physical} or return;
    my $ps = -s $pf or return;
    my $data = $picasa->{pics}{$pf} or return; # data of this picture
    my $x = $data->{width} or return;
    my $y = $data->{height} or return;
    my $scale = $mx / $x < $my / $y ? $mx / $x : $my / $y;
    my($X, $Y) = $image->PhotoXYfull($pf, map { int } $scale * $x, $scale * $y)
	or return;
    $full->title("PG: $picasa->{file}{file}");

    $canvas->delete('exif');
    $canvas->delete('caption');
    $canvas->delete('info');
    $canvas->delete('faces');
    return unless $conf->{info};

    if (my $info = $exif->ImageInfo($pf)) {
	my @info;
	push @info, $info->{Make} if $info->{Make};
	push @info, $info->{Model} if $info->{Model};
	push @info, "$info->{ExposureTime}s" if $info->{ExposureTime};
	push @info, $info->{FocalLength} if $info->{FocalLength};
	push @info, "($info->{FocalLengthIn35mmFormat})"
	    if $info->{FocalLengthIn35mmFormat};
	push @info, "f/$info->{FNumber}" if $info->{FNumber};
	push @info, "ISO: $info->{ISO}" if $info->{ISO};
	push @info, $info->{Flash} if $info->{Flash};
	$canvas->createTextBox($mx, $my, $conf->{bg},
			       -justify => 'right',
			       -anchor => 'se',
			       -font => $med,
			       -fill => $conf->{text},
			       -text => (join " \n", @info),
			       -tags => 'exif');
    }
    $canvas->createTextBox($mx, 0, $conf->{bg},
			   -justify => 'right',
			   -anchor => 'ne',
			   -font => $med,
			   -fill => $conf->{text},
			   -text => &stats($this, $mx, $my),
			   -tags => 'info');
    (my $file = $this->{file}) =~ s/$this->{time}-//;
    my $text = "$this->{dir}\n$file\n$this->{time}";
    $text .= "\n$this->{endtime}" if $this->{time} ne $this->{endtime};
    my $tmp = &nums($this);
    $text .= "\n($tmp)" if $tmp;
    $text .= "\n" . join "\n", @{$this->{tags}} if @{$this->{tags}};
    $canvas->createTextBox(0, 0, $conf->{filtering} ? 'red' : $conf->{bg},
			   -anchor => 'nw',
			   -font => $med,
			   -fill => $conf->{filtering} ? 'white' : $conf->{text},
			   -text => $text,
			   -tags => 'info');
    $text = join "\n", map { $picasa->{album}{$_}{name} } @{$this->{albums}};
    $canvas->createTextBox(0, $my, $conf->{bg},
			   -anchor => 'sw',
			   -font => $med,
			   -fill => $conf->{text},
			   -text => $text,
			   -tags => 'info');

    my $total = @{$picasa->{dir}{children}};
    my $cur = $picasa->{index} + 1;
    $canvas->createTextBox($mx / 2, 0, $conf->{bg},
			   -anchor => 'n',
			   -font => $med,
			   -fill => $conf->{title},
			   -text => "$cur / $total",
			   -tags => 'info');
    my $bar = $mx / $total;	# disappearing "progress" bar!
    $bar = 5 if $bar < 5;	# TODO: all info could disappear...
    my $id = $canvas->createLine($bar * $picasa->{index}, 2,
    				 $bar * $picasa->{index} + $bar, 2,
    				 -fill => $conf->{title},
    				 -width => 5,
    				 -tags => 'info');
#    $full->after(3000, sub { $canvas->delete($id) });

    if ($this->{caption}) {
	$canvas->createTextBox($mx / 2, $my, $conf->{bg},
			       -width => $mx,
			       -anchor => 's',
			       -font => $med,
			       -fill => $conf->{title},
			       -text => $this->{caption},
			       -tags => 'caption');
    }

    for my $f (@{$this->{faces}}) { # named rectangles around faces!
	next unless ref $f;
	my($id, $l, $t, $r, $b) = @$f;
	my $name = $picasa->contact2person($id);
	$canvas->createRectangle($X + $l * $x * $scale, $Y + $t * $y * $scale,
				 $X + $r * $x * $scale, $Y + $b * $y * $scale,
				 -outline => $conf->{face},
				 -tags => 'faces');
	$canvas->createText($X + $l * $x * $scale + 2, $Y + $t * $y * $scale,
			    -anchor => 'nw',
			    -font => $med,
			    -fill => $conf->{face},
			    -text => $name,
			    -tags => 'faces');
	if (my $birth = $conf->{birthday}{$id} # show ages of faces :-)
	    || $conf->{birthday}{$name}) {
	    my($yr, $m, $d) = split '/', $birth;
	    my($Yr, $M, $D) = split /\D+/, $this->{time};
	    my($year, $mon, $day) = Delta_YMD($yr, $m, $d, $Yr, $M, $D);
	    warn "age $id/$name: $Yr, $M, $D - $yr, $m, $d = $year, $mon, $day\n"
		if $conf->{debug};
	    my $age;
	    if ($year < 1 && $mon < 3) {
		$age = sprintf "%dd", Delta_Days($yr, $m, $d, $Yr, $M, $D);
	    } elsif ($year < 3) {
		$age = sprintf "%dm", int($year * 12 + $mon);
	    } else {
		$age = sprintf "%d", int($year + $mon / 12 + $day / 365);
	    }
	    $canvas->createText($X + $r * $x * $scale - 2, $Y + $b * $y * $scale,
				-justify => 'right',
				-anchor => 'se',
				-font => $big,
				-fill => $conf->{face},
				-text => $age,
				-tags => 'faces');
	}
    }
}

# read $f file into $w image scaled to size $x / $y, with cacheing
sub Tk::PhotoXYthumb {
    my ($w, $f, $x, $y, $c) = @_;
    $f && $x && $y or return;
    my $ext = 'jpg';		# jpg is smaller than png
    my $cfn = "$conf->{cache}/$f-$x-$y.$ext";
    unless (!$rebuildthumb{$f} && -f $cfn && -M _ <= -M $f) {
	my($dir, $file) = Picasa::dirfile($cfn);
	-d $dir or mkpath $dir or warn "can't mkdir $dir: $!\n";
	my $geo = "${x}x${y}";
	if ($c) {
	    system qw/convert -fill white -gravity center -background/,
	    '#0006', '-size', $tpx . 'x' . int($tpy / 6), "caption:$c", $f,
	    '-thumbnail', $geo, qw/+swap -gravity south -composite/, $cfn;
	} else {
	    system qw/convert -thumbnail/, $geo, $f, $cfn;
	}
	my($time) = (stat $f)[9]; # timestamp same as source file
	utime $time, $time, $cfn;
    }
    $w->blank;
    $w->read($cfn, -to => int(($tpx - $x) / 2), int(($tpy - $y) / 2));
}

# 3 @pics stacked into $w image of size $x / $y, with cacheing as $f
sub Tk::StackXYthumb {
    my ($w, $f, $x, $y, $label, @pic) = @_;
    $f && $x && $y && @pic or return;
    my $ext = 'png';		# jpg isn't transparent; use png
    my $cfn = "$conf->{cache}/$f-$x-$y.$ext";
    
    unless (!$rebuildthumb{$f} &&
	    -f $cfn && @pic == grep { -M $_ >= -M $cfn } @pic) {
	my($dir, $file) = Picasa::dirfile($cfn);
	-d $dir or mkpath $dir or warn "can't mkdir $dir: $!\n";
	my $geo = sprintf "%.0fx%.0f", $x * 2 / 3, $y * 2 / 3;
	my @cmd = ('convert', '-size', "${x}x${y}", 'xc:#0000');
	push @cmd, '(', $pic[0], '-thumbnail', $geo, ')',
	qw/-gravity northwest -composite/;
	push @cmd, '(', $pic[1], '-thumbnail', $geo, ')',
	qw/-gravity center -composite/
	    unless $pic[0] eq $pic[1];
	push @cmd, '(', $pic[2], '-thumbnail', $geo, ')',
	qw/-gravity southeast -composite/
	    unless $pic[2] eq $pic[1] or $pic[2] eq $pic[0];
	push @cmd, qw/-fill white -gravity center -background/, '#0006', '-size',
	sprintf("%.0fx%.0f", $x, $y / 5), "caption:$label", '-composite', $cfn;
	system @cmd;
	my($time) = sort { $b <=> $a } map { (stat $_)[9] } @pic; # latest
	utime $time, $time, $cfn;
    }
    $w->blank;
    $w->read($cfn);
}

# read $f file into $w image scaled to size $x / $y
BEGIN {
    my $id = 10000;
    my $ext = 'jpg';		# full format
    sub Tk::PhotoXYfull {
	my ($w, $f, $x, $y) = @_;
	$f && $x && $y or return;
	my $cfn = "$conf->{cache}/tmp-$$-" . $id++ . ".$ext";
	my $geo = "${x}x${y}";
	system "convert", "-resize", $geo, $f, $cfn;
	my($l, $t) = (int(($mx - $x) / 2), int(($my - $y) / 2));
	$w->blank;
	$w->read($cfn, -to => $l, $t);
	unlink $cfn;
	return $l, $t;
    }
}

# createText on a box of given color $c
sub Tk::Canvas::createTextBox {
    my($w, $x, $y, $c, %opt) = @_;
    my $t = $w->createText($x, $y, %opt);
    my @opt;
    push @opt, (-tags => $opt{'-tags'}) if $opt{'-tags'};
    $w->createRectangle($w->bbox($t), -fill => $c, @opt);
    $w->raise($t);
}

# Toggle fullscreen by hiding decorations off-screen. I prefer this to
# overrideredirect(1) which loses focus and Alt-Tab WM controls.
{
    my %geom;			# remember size/pos before full screen
    sub togglefullscreen {
	my($w) = @_;
	my($offx, $offy) = ($w->rootx, $w->rooty);
	warn "$w: + $offx + $offy\n" if $conf->{debug};
	if ($offx || $offy) {	# not at 0, 0 == not full screen
	    $geom{"$w"} = $w->geometry; # size/location to restore
	    my($x, $y) = ($mw->screenwidth, $mw->screenheight);
	    $w->minsize($x, $y); $w->maxsize($x, $y);
	    $w->geometry(sprintf "%dx%d+0+0", $x, $y);
	    $w->update;
	    ($offx, $offy) = ($w->rootx, $w->rooty);
	    warn "$w: $x x $y +- $offx +- $offy\n" if $conf->{debug};
	    $w->geometry(sprintf "%dx%d+-%d+-%d", $x, $y, $offx, $offy);
	    $w->attributes(-fullscreen => 1) # works on mythbuntu!
		if grep /-fullscreen/, $w->attributes;
	    $w->raiseFocus;
	} else {		# at 0, 0 == already full screen
	    $w->minsize(1, 1);
	    $w->geometry($geom{"$w"});
	    $w->attributes(-fullscreen => 0)
		if grep /-fullscreen/, $w->attributes;
	    $w->raiseFocus;
	}
    }
}

# raise window $w and ensure it has focus
sub Tk::raiseFocus {
    my($w) = @_;
    $w->deiconify;
    $w->raise;			# causes menu @ 0,0 on Windows??!
    $w->focus;
    $w->grab;
    $w->update;
    $cur = $w unless $w eq $menu; # focus to restore after menu
}

__END__
:endofperl
