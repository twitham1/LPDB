#!/usr/bin/perl

use strict;
use warnings;
use Prima qw(Application MsgBox);

my $MIN = 8;
my $MAX = 120;
my $BORD = 20;
my $EACH = $MAX + $BORD * 2;
my $w;				# window
my $title;			# window title string
my $icon = icon();		# current icon

# Under x11 with XFCE, this must be set before first window, and
# changes have no effect!  -twitham
$::application->icon($icon)
    unless grep /noicon/, @ARGV;

my $timer = Prima::Timer->create(
    timeout => 1000,
    onTick => sub { newicon(0) }
    );
$w = Prima::MainWindow->new(
    text      => 'Prima Icon Test',
    size      => [ $EACH * 3, $EACH ],
    menuItems =>
    [['~Icon' =>
      [
       ['Random ~Application Icon', 'a', ord 'a', sub { newicon(1) } ],
       ['Random Window ~Icon',      'i', ord 'i', sub { newicon(0) } ],
       ['@c', '~Cycle Window Icon', 'c', ord 'c', sub { $_[2] ? $timer->start : $timer->stop }],
       ['New ~Window',              'w', ord 'w', sub { message("icon from parent?") }],
       ['@save', '~Save to Files',  's', ord 's', sub {}],
       ['@all', 'All ~Depths',      'd', ord 'd', sub {}],
       ['~Quit',                    'q', ord 'q', sub { exit } ],
      ]
     ]],
    onPaint => sub {
	my($self, $canvas) = @_;
	$canvas->clear;
	$canvas->color(cl::Red);
	for (my $i = -1 * $EACH; $i < $EACH * 4; $i += $BORD) {
	    $canvas->line($i, 0, $i + $EACH, $EACH);
	    $canvas->line($i, 0, $i - $EACH, $EACH);
	}
	my $icon = $self->icon;
	my($img, $mask) = $icon->split;
	$canvas->put_image($BORD,		$BORD, $icon);
	$canvas->put_image($EACH + $BORD,	$BORD, $img);
	$canvas->put_image($EACH * 2 + $BORD,	$BORD, $mask);
	if ($self->menu->checked('save')) {
	    $icon->save("icon.png"); # prove img/mask are right in ./*.png:
	    $img->save("img.png");
	    $mask->save("mask.png");
	}
    },
    );

run Prima;

sub newicon {	  # replace application or window icon with random one
    my($app) = @_;
    $icon = icon();
    $app ? $::application->icon($icon) : $w->icon($icon);

    my $t = $icon->type;
    $w->text(($app ? 'application' : 'window') . " $title -> type $t");
    $w->repaint;
}

sub icon { # return an icon of 4 random squares in random size and bpp
    my $x = int(rand($MAX - $MIN) + $MIN);
    my $y = int(rand($MAX - $MIN) + $MIN);
    my @bpp = $w && $w->menu->checked('all')
	? qw/1 4 8 16 24 32 64 128/ # all of im::* bit depths
	: qw/1 4 8 24 32/;	# only valid visual depths
    my $t = $bpp[rand @bpp];
    $title = "$t bpp ($x x $y)";
    my $i = Prima::Icon->new(
	width => $x,
	height => $y,
	type   => $t,
	);
    $i->begin_paint;
    $i->color(cl::Black);
    $i->bar(0, 0, $x, $y);
    $i->color(cl::Blue);
    $i->bar(rand($x), rand($y), rand($x), rand($y));
    $i->color(cl::Red);
    $i->bar(rand($x), rand($y), rand($x), rand($y));
    $i->color(cl::Green);
    $i->bar(rand($x), rand($y), rand($y), rand($y));
    $i->color(cl::Yellow);
    $i->bar(rand($x), rand($y), rand($x), rand($y));
    $i->end_paint;
    return $i;
}
